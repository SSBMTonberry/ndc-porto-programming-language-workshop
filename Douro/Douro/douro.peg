@namespace Douro
@using Douro.Values
@using Douro.Statements
@classname DouroParser

program<DouroProgram>
	= ws stmt:statement ws rest:program {
		rest.Insert(stmt)
	}
	/ ws { new DouroProgram() }

statement<Statement>
	= function
	/ print
	/ assign
	/ return

function <Statement>
	= "function" _ name:identifier __ '(' __ args:identifier_list __ ')' __ '{' __ EOL+
		__ body:statement_list EOL*
		__ '}'
	{
			new Assign(name, new Function(args, body))
	}

statement_list <List<Statement>>
	= __ head:statement EOS+ tail:statement_list {
		(new List<Statement> { head }).Concat(tail).ToList()
	}
	/ __ stmt:statement { new List<Statement> { stmt } }

return<Statement>
	= "return" _ expr:expression {
		new Return(expr)
	}


EOS = EOL // End of statement

identifier = ("" [a-zA-Z_][a-zA-Z0-9_]*)

assign<Statement> =
	"let" _ name:identifier __ "=" __ expr:expression {
		new Assign(name, expr)
	};

print<Statement> =
	"print" _ expr:expression {
		new Print(expr)
	};

expression <Expr> = sum

sum <Expr> -memoize
	= lhs:product __ "+" __ rhs:sum	{ new Binary(lhs, rhs, Operator.Add) }
	/ lhs:product __ "-" __ rhs:sum	{ new Binary(lhs, rhs, Operator.Subtract) }
	/ product

product <Expr> -memoize
	= lhs:product __ "/" __ rhs:primary { new Binary(lhs, rhs, Operator.Divide) }
	/ lhs:product __ "*" __ rhs:primary { new Binary(lhs, rhs, Operator.Multiply) }
	/ primary

primary
	= number
	/ print_expr
	/ function_call
	/ lookup

print_expr <Expr>
	= "print" _ expr:expression { new PrintExpr(expr) }

function_call <Expr>
	= name:identifier __ '(' __ args:arg_list __ ')' { new FunctionCall(name, args) }

arg_list<List<Expr>>
	= head:expression __ ',' __ tail:arg_list
		{ (new List<Expr> { head }).Concat(tail).ToList() }
	/ arg:expression { new List<Expr> { arg } }
	/ "" { new List<Expr>() }

identifier_list<List<string>>
	= head:identifier __ ',' __ tail:identifier_list
		{ (new List<string> { head }).Concat(tail).ToList() }
	/ name:identifier { new List<string> { name} }
	/ "" { new List<string>() }

lookup <Expr> = name:identifier {
		new Lookup(name)
	}

number <Expr> =
	digit:("" [0-9]+) {
		new Number(digit)
	}

_ = [ \t]+ ;
__ = [ \t]* ;
ws = [ \t\n\r]* ;
EOL = [\n\r]+

EOF = !.
    / unexpected:.
        #error{ $"Unexpected '{unexpected}' at line {state.Line}, col {state.Column - 1}" }
